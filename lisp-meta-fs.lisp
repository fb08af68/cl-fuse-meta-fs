; Part of cl-fuse FUSE bindings for Common Lisp
; Distributed as free software under Lisp Library General Public License
; Distributed AS IS with NO WARRANTY

(defpackage :cl-fuse-meta-fs
  (:use 
   :common-lisp
   :cl-fuse
   :iterate
   :bordeaux-threads
   )
  (:export #:run-lisp-meta-fs
           #:mk-file
           #:mk-dir
           #:mk-symlink
           #:mk-symlinker
           #:mk-generator
           #:mk-pair-generator
           #:mk-creator
           #:mk-dir-remover
           #:fmt
           #:id
           #:cached-expr
           #:*object-cache-duration*
	   #:*meta-fs-name-encoding*
           )
  )

(in-package :cl-fuse-meta-fs)

#+nil 
(
  (:type :file name (contents) (writer data) (remover))
  (:type :dir name (contents))
  (:type :symlink name (contents) (remover))
  (:type :generate (parser x) (predicate x) (lister) (contents x))
  (:type :creator (create-file name) (create-dir name))
  )

(defparameter *meta-fs-name-encoding* :full-range)
(defun decode-name (n) 
  (cond
   ((stringp n) n)
   ((arrayp n) (cl-fuse::octets-to-string n *meta-fs-name-encoding*))
   ((listp n) (format nil "狺磲疸狎т邈镤瀛钺礤瞟┅ㄦ矧磲铋㈠蝌矧溴泔溟铉钺礤湖簋ア瞟┅ㄤ彐磲泸黼骈戾钺礤泔铘孱趔镳糸镱犰黩轸弪铋飑蝈盹鲥铋飑箝铋飑ㄥ邈豸徕戾铋飑噜ê豉疱烘殪侯犴ㄤ邈镤瀛钺礤钺礤恒镱翦铘灬礅溽ī泔铘孱趔瑚蜷翦ㄣ镱è铛祆黩轸弪铋飑è簌礅镬黩轸弪唰黩轸弪啜灬礅溽ìㄣ狎黩轸弪┅ㄤ邈灬蝈ㄩ珙矧徕戾ㄣ狎黩轸弪┅括沅黩轸弪┅候屙秭弪麒孱蝈盹鲥啜灬礅溽ī蝈盹鲥颟后辁ㄩ箝啜灬礅溽ī箝濠哄邈豸徕戾屮邈豸徕戾┅ㄤ彐磲泸黼溟钺礤腴钿蝈篝泔铘孱趔噜ê豉疱轰轵侯犴ㄤ邈镤瀛钺礤钺礤恒镱翦铘灬礅溽īㄣ狍腴钿ê牾篝啜扉篝楞镱翦铘螬ê弼犰啜痱镧楞镱翦铘螬┅┅ㄤ彐磲泸黼簌盱轭钺礤泔铘孱趔镳糸镱犰蝈盹鲥铋飑噜ê豉疱后盱轭侯犴ㄤ邈镤瀛钺礤钺礤恒镱翦铘灬礅溽īㄤ邈镤瀛钺礤泔铘孱趔┅候屙秭弪麒孱蝈盹鲥啜灬礅溽ī蝈盹鲥颟┅ㄤ彐磲泸黼珏铄蜥麸鲠疳蝮弪痱邃殂狒扉篝弪泔铘孱趔噜ê豉疱虹孱弪狒吼狎箦ㄩ簌礅镬疳蝮弪唰疳蝮弪啜灬礅溽ì鲠颟疳蝮弪┅吼蝈溟汜翦ㄩ簌礅镬痱邃殂狒濠唰痱邃殂狒啜灬礅溽ì鲠颟痱邃殂狒濠红轶翦灬礅溽ī扉篝弪恒镱翦铘灬礅溽ì鲠颟泔铘孱趔┅ㄤ彐鲠汜汨瀛篝矧徵濯磲脲栳箬翎忪呼弩у聃犰┅ㄤ彐鲠汜汨瀛篝矧徵瀛祜汶ㄢ矧溴狨翳蝈徜蠛磲脲祜汶汜汨瀛篝矧徵濯┅ㄤ彐躅汜汨邃ㄦ脲脲ㄤ躜狒轱暴躅戾篌漉蜥糸镱蝈趱蝾骝镯汜汨邃ㄦ躅汜祆姗┅戾舄è溽翎鏖翳祜汶桢熹í汜汨瀛篝矧徵瀛祜汶ㄧ弭栳箬脲汜汨瀛篝矧徵濯┅眙轫ㄦ轵篝溽翎┅鲠箦泔钿溽翎┅铒ㄧ弭躅轹弪筢飙糸礤┅ㄩㄡ钿眙轫冀ō铒眙轫濠漉蜥糸镱┅痱镧鲠飑痱镧箦翩鲠ㄦ躅汜祆姗鏖翳祜汶桢熹í汜汨瀛篝矧徵瀛祜汶箦翩ㄧ弭栳箬脲汜汨瀛篝矧徵濯扉篝铒鲠飑┅鲠┅┅ㄤ彐磲泸汜汨邃屮痱ㄥ脲脲ㄤ躜狒轱暴ㄥ鲠飙脲铋飑戾舄è簌ㄧ孱簌愆┅啜汜汨邃灬礅溽ī濠扉篝К簌ㄩ弼犰脲脲唰脲┅轰躜狒轱漉蜥糸镱┅ㄤ彐磲泸黼疳轵珏铄蜥麸鲠扉篝弪镳糸镱犰泔铘孱趔噜ê豉疱虹孱弪狒吼狎箦灬礅溽ì鲠颟ㄦ轭鲠戾ì鲠扉篝鲠颟扉篝弪呼弩у聃犰弘妁ф轵篝┅吼蝈溟汜翦灬礅溽ì鲠颟ㄦ轭ㄦ轵篝鲠颟扉篝弪呼弩у聃犰弘妁ф轵篝┅红轶翦灬礅溽ī戾ì鲠扉篝铋飑扉篝弪┅恒镱翦铘ㄩ泔铘孱趔啜灬礅溽ì鲠颟泔铘孱趔啜灬礅溽ì鲠颟箦泔钿鲠颟┅┅ㄤ彐磲泸黼泸遽麸鲠泸遽翦骈戾泸遽翦溟颟啜扉篝呼疱恒蝈狒矧括麒孱泸遽翦骈戾扉篝恒蝈狒瀛骈戾啜灬礅溽ì鲠颟泸遽翦骈戾┅括麒孱泸遽翦溟扉篝恒蝈狒瀛溟啜灬礅溽ì鲠颟泸遽翦溟颟┅┅ㄤ彐磲泸黼簌盱轭脲鲠翎蜱弭鲠蝈篝怙澌花骢箦泔眇灬轭Ⅲ盱轭脲蚝簋ア鲠翎蜱弭鲠怙澌啜扉篝呼疱后盱轭脲烘躅泗轱灬礅溽ì鲠翎蜱弭鲠颟棱镤┅ㄤ彐磲泸黼溟颦蝈盹鲥ㄢ镤啜扉篝呼疱候屙秭弪烘躅泗轱灬礅溽ī怙澌┅ㄤ彐磲泸骓鲠颟啜骘蝽狒铋鲠颟ㄤ彐磲泸殇é蝈篝啜鲠祯弩励┅ㄤ彐躅筢驽汜祆ㄦ蝈篝狎珞ㄩ珙矧瀛弪蝻蝮蝈趱蝾骝镯筢驽汜祆鲠祯弩ㄡ痧禊ф躅汜祆狎珞舂┅鲠祯弩铋铋飑ㄤ彐鲠镡赍泗汜汨瀛漉蜥糸镱铋飑ㄤ彐鲠镡赍泗汜汨濯磲脲栳箬翎忪呼弩у聃犰┅ㄤ彐鲠镡赍泗汜汨瀛祜汶ㄢ矧溴狨翳蝈徜蠛磲脲祜汶镡赍泗汜汨濯┅ㄤ彐躅镡赍泗汜汨瀛蝈趄殄鲥脲铒鳗花骢箦泔眇灬轭⒚徙桢箦狎汨螈脲戾舄è溽翎鏖翳祜汶桢熹í镡赍泗汜汨瀛祜汶ㄧ弭栳箬脲镡赍泗汜汨濯┅眙轫ㄣ狎溽翎┅ㄩㄡ钿眙轫冀ō铒眙轫濠镡赍泗汜汨瀛漉蜥糸镱┅痱镧花骢箦泔眇灬轭⒚徙桢栝艉螈脲溽翎鲠祯弩箦泔钿溽翎舂痱镧花骢箦泔眇灬轭⒚徙桢黹篌螈脲鲠祯弩铋铋飑┅┅ㄤ彐躅珏舡镡赍泗疳翳轭轸獒镳糸镱犰蝈霏疳翳犰蝈徜铋飑花骢箦泔眇灬轭⑶弭糸铉镡赍泗骝镯ㄣ徙桢螬疳翳轭轸獒镡赍泗汜汨瀛漉蜥糸镱戾舄è汜汨瀛痱镢弩箝铉ㄡ钿镡赍泗汜汨瀛漉蜥糸镱ㄧ弭轭轸獒候镲舂┅花漉眄ㄦ躞瀛泔眇灬轭⒚徙桢轭躞蹇螈汜汨瀛痱镢弩箝铉┅蝈霏疳翳麒孱镡赍泗汜汨瀛漉蜥糸镱蝈鲥蝮疳翳┅花漉眄麒孱汜汨瀛痱镢弩箝铉ㄦ躞瀛泔眇灬轭疳翳蝈鲥蝮邃┅铒麒孱镡赍泗汜汨瀛漉蜥糸镱ㄧ弭躅轹弪筢飙糸礤┅花漉眄麒孱汜汨瀛痱镢弩箝铉ㄦ躞瀛泔眇灬轭Ⅳ轫顼簪┅ㄤ蹴睐麒孱汜汨瀛痱镢弩箝铉花骢箦泔眇灬轭⒚徙桢祜镳ㄩ翦ㄦ矧镱蝈霏疳翳眭祠轲戾鲠祯瀛忾钿鲠篚沣弩螬镡赍泗汜汨瀛蝈趄殄鲥铒鳗麒孱篚沣弩蝈趱蝾骝镯珏舡镡赍泗ㄧ弭镡赍泗篚怏羼疳翳戾铉翳皓鲠┅┅鲠ㄢ祜汶珏舡镡赍泗汜汨邃花骢箦泔眇灬轭⑵轭犰蝈趄殄鲠膦ㄣ镱è铛祆疳翳轭轸獒飑è羼ㄧ弭轭轸獒呼疱哄蝌矧轭轸獒飑è羼ㄧ弭轭轸獒呼疱烘殪濠啜呼疱哄蝌矧恒镱翦铘沆骢箦哄蝌矧盼显纳药戾舄è翳瀛钺礤ㄣ狎疳翳┅ㄩ翦ㄦ矧孱趄轭ㄦ躅汜祆ㄧ弭轭轸獒恒镱翦铘螬┅ㄣ狍ㄧ弭孱趄呼疱è烘殪轰轵后盱轭氅麒孱ㄥ聃犰ㄧ弭孱趄侯犴濠翳瀛钺礤蝈趱蝾骝镯珏舡镡赍泗汜汨邃ㄧ弭镡赍泗ㄣ潋疳翳孱趄┅┅ê珏铄蜥翦眭祠轲戾鲠祯瀛忾钿鲠疳蝮邃筢驽汜祆ㄧ弭孱趄吼狎箦颟翳瀛钺礤麒孱ㄡ钿疳蝮邃筢驽汜祆ㄧ弭孱趄吼蝈溟汜翦鲠飑蝈趱蝾骝镯珏舡镡赍泗汜汨邃ㄧ弭镡赍泗ㄣ潋疳翳ㄦ躅汜祆ㄧ弭孱趄恒镱翦铘螬鲠飑┅┅铋飑┅啜呼疱哄蝌矧恒镱翦铘沆骢箦哄蝌矧盼吓卧┅┅ㄤ邈灬蝈ㄩ珙矧漉眄┅麒孱ㄡ钿蝈霏疳翳镡赍泗汜汨瀛漉蜥糸镱鏖翳祜汶桢熹í镡赍泗汜汨瀛祜汶箦翩ㄧ弭栳箬ㄡ痧孱蝈霏疳翳蝈霏疳翳犰蝈徜镡赍泗汜汨濯扉篝铒鲠飑┅花骢箦泔眇灬轭⑾怅邈顼艉螈疳翳鲠飑鲠┅ㄤ彐躅扉篝溟蝈泗矧镡脲疳翳铋疳翳玳鲥瞽皓戾舄è扉篝轭绛豉疱ㄧ弭镡呼疱┅扉篝轭绛溽翎ㄣ狍扉篝轭绛豉疱ê溟ㄦ躅汜祆ㄧ弭镡恒镱翦铘螬┅ê珏铄蜥翦ㄦ躅汜祆ㄧ弭镡红轶翦颟┅铋飑┅ㄥ铘蜷弩ㄣ狍扉篝轭绛豉疱ê溟扉篝轭绛溽翎ê珏铄蜥翦磲疸狎ㄧ弭镡恒镱翦铘螬扉篝轭绛溽翎┅铋飑┅铒麒孱疳翳玳鲥瞽ㄧ弭躅轹弪筢飙糸礤┅ㄦ躞瀛泔眇灬轭⑻轶糸铉溟蝈泗矧狒疳翳簋ハ怅邈轶簋ヅ铘蜷弩狎簋%" path obj entries)
        (iter (for entry in entries)
              (iter:with result = nil)
	      (iter:with rev-path := (reverse path))
	      (iter:for et := (getf entry :type))
	      ;(fuse-complain "Looking at entry s%Type is s%" entry et)
	      (when (and 
		      *object-cache-duration* path-given-p
		      (or (eq et :file) (eq et :dir) (eq et :symlink)))
		;(fuse-complain "Pushing into cache s s" entry rev-path)
		(with-lock-held 
		  (*object-cache-lock*)
		  (setf (gethash (cons (getf entry :name) rev-path ) *object-cache*)
			(list now entry))))
              (case et
                    ((:dir :symlink) 
                     (push (list (getf entry :name) (getf entry :type))
			   result))
		    ((:file)
                     (push (getf entry :name) result))
                    (:generate
                     (setf result (append 
				    (if 
				      path-given-p
				      (list-directory entry :path path) 
				      (list-directory entry))
				    result)))
                    (t nil))
	      ;(fuse-complain "Listed directory s, result is%簋%----- END OF LISTING ----" path result)
              (finally (return result)))))

(defun launch-creator (obj kind name)
  (when (eq (getf obj :type) :dir)
        (iter (for entry in (funcall (getf obj :contents)))
              (for func := (getf entry kind))
              (for e-type := (getf entry :type))
              (when 
               (and (eq e-type :creator)
                    func
                    (funcall func name)
                    )
               (return t)
               )
              )))

(defun get-dir-remover (obj)
  (when (eq (getf obj :type) :dir)
    (iter
      (for entry in (funcall (getf obj :contents)))
      (for e-type := (getf entry :type))
      (for func := (getf entry :function))
      (when 
	(and
	  (eq e-type :remover)
	  func)
	(return func)))))

(defun launch-symlinker (obj name content)
  (when (eq (getf obj :type) :dir)
    (iter (for entry in (funcall (getf obj :contents)))
	  (for func := (getf entry :function))
	  (for e-type := (getf entry :type))
	  (when 
	    (and (eq e-type :symlinker)
		 func
		 (funcall func name content)
		 )
	    (return t)
	    )
	  )))

(defvar *description*)

(defmacro make-op (f args &rest body)
  `(defun ,f ,(append `(path) args `(&rest more-args))
          (declare (ignorable more-args))
	  ;(fuse-complain "make-op-created operation s called with path s%" ',f path)
          (let* ((obj (get-object path *description*))
                 (result (progn ,@body))
                 )
                result
                )))

(make-op directoryp ()
	 ;(fuse-complain "Is-directory? s s" path obj)
  (eq (getf obj :type) :dir))

(make-op directory-content ()
	 ;(fuse-complain "List s s" path obj)
  (list-directory obj :path path))

(make-op symlinkp ()
  (eq (getf obj :type) :symlink))

(make-op symlink-target ()
  (when (eq (getf obj :type) :symlink)
      (funcall (getf obj :contents))))

(defvar *write-cache* (make-hash-table :test 'equal))
(defvar *write-cache-lock* (bordeaux-threads:make-lock "*write-cache*"))

(make-op file-read ()
 (when (eq (getf obj :type) :file)
   (or (with-lock-held (*write-cache-lock*) (gethash path *write-cache*))
       `(:offset 0 ,(funcall (getf obj :contents))))))

(make-op 
  file-size ()
  (when (eq (getf obj :type) :file)
    (let*
      (
       (size (getf obj :size))
       (content (and (not size) (funcall (getf obj :contents))))
       (content
	 (if (stringp content)
	   (cl-fuse::string-to-octets content :full-range)
	   content))
       )
      (if size (funcall size) (length content)))))

(make-op file-write-whole (data)
  (when (getf obj :writer)
        (with-lock-held
	  (*write-cache-lock*)
	  (setf (gethash path *write-cache*) data))
        (length data)
        ))

(make-op file-flush ()
  (let*
    (
     (wc (with-lock-held (*write-cache-lock*) (gethash path *write-cache*)))
     )
    (if 
      wc
      (progn
	(with-lock-held (*object-cache-lock*) 
			(remhash (reverse path) *object-cache*))
	(when
	  (getf obj :writer)
	  (funcall 
	    (getf obj :writer)
	    wc)))
      t
      )))

(make-op file-release ()
  (with-lock-held (*write-cache-lock*) (remhash path *write-cache*))
  t 
  )

(make-op file-unlink ()
  (when (getf obj :remover)
    (with-lock-held
      (*object-cache-lock*)
      (remhash (reverse path) *object-cache*)
      (remhash (cdr (reverse path)) *object-cache*))
    (funcall (getf obj :remover))))

(make-op dir-unlink ()
  (let*
    ((f (get-dir-remover obj)))
    (when f
      (with-lock-held
	(*object-cache-lock*)
	(remhash (reverse path) *object-cache*)
	(remhash (cdr (reverse path)) *object-cache*))
      (funcall f)
      )))

(defun safe-subseq (seq start end)
  (cond
   ((>= (length seq) end start)
    (subseq seq start end))
   ((= (length seq) 0) 
    (make-array (- end start) :element-type '(unsigned-byte 8)))
   ((>= end start)   
    (let* ((new-seq 
            (make-array (- end start) :element-type '(unsigned-byte 8))))
          (loop for i from start to (- (length seq) 1) 
                for j from 0 to (- end start 1)
                do
                (progn
                 (setf (elt new-seq j) (elt seq i))
                 )
                )
          new-seq
          ))
   (t nil)
   ))

(make-op file-truncate (offset)
  (with-lock-held
    (*write-cache-lock*)
    (setf
      (gethash path *write-cache*)
      (safe-subseq
	(gethash path *write-cache* 
		 (make-array 0 :element-type '(unsigned-byte 8)))
	0 offset)))
  t
  )

(make-op file-create ()
  (with-lock-held
    (*object-cache-lock*)
    (remhash (reverse path) *object-cache*))
  (launch-creator 
   (get-object (butlast path) *description*)
   :create-file
   (car (last path)))
  )

(make-op dir-create ()
  (with-lock-held
    (*object-cache-lock*)
    (remhash (reverse path) *object-cache*))
  (launch-creator 
   (get-object (butlast path) *description*)
   :create-dir
   (car (last path)))
  )

(make-op file-writeable-p ()
  (getf obj :writer))

(make-op file-executable-p ()
	 (getf obj :executable))

(make-op 
  symlink (content)
  (with-lock-held
    (*object-cache-lock*)
    (remhash (reverse path) *object-cache*))
  (launch-symlinker
    (get-object (butlast path) *description*)
    (car (last path))
    content))

(defun run-lisp-meta-fs (description &optional (target-path "/tmp/test") (call-manager nil)
  (thread-pool-size 8) (extra-fuse-args nil))
  (when call-manager (setf (pcall:thread-pool-size) thread-pool-size))
  (setf *description* `(:type :dir :contents ,(constantly description) :root t))
  (ignore-errors (ensure-directories-exist (concatenate 'string target-path "/")))
  (cl-fuse:fuse-run `("meta-fs" ,target-path 
                                "-o" "attr_timeout=1s" 
                                "-o" "entry_timeout=1s" 
                                "-o" "ac_attr_timeout=1s" 
                                "-o" "intr"
				,@ extra-fuse-args
				)
                    :directoryp #'directoryp
                    :directory-content #'directory-content
                    :symlink-target #'symlink-target
                    :symlinkp #'symlinkp
                    :file-read #'file-read
                    :file-size #'file-size
                    :file-write-whole #'file-write-whole
                    :file-writeable-p #'file-writeable-p
                    :file-executable-p #'file-executable-p
                    :file-flush #'file-flush
                    :file-release #'file-release
                    :truncate #'file-truncate
                    :file-create #'file-create
                    :mkdir #'dir-create
                    :unlink #'file-unlink
                    :rmdir  #'dir-unlink
		    :symlink #'symlink
                    :call-manager (if call-manager 
				      (let*
					(
					 (tasks nil)
					 (caller
					   (lambda 
					     (f &rest x) 
					     (declare (ignore x))
					     (loop
					       while (>= (length tasks) thread-pool-size)
					       do (let*
						    (
						     (first-finished (apply 'pcall:select-one tasks))
						     )
						    (when first-finished
						      (pcall:join first-finished)
						      (setf tasks (remove first-finished tasks)))
						    )
					       )
					     (push (pcall:pcall f) tasks)
					     ))
					 )
					caller)
                                      (lambda (f &rest x) 
                                              (declare (ignore x)) 
                                              (funcall f))
                                      )
                    ))
